///////////////////////////////////////////////////////////////////////////////
// Tracing forks via clone
///////////////////////////////////////////////////////////////////////////////

tracepoint:syscalls:sys_enter_clone
{
  $task = (struct task_struct *)curtask;
  // Ensures that we don't process forks of threads
  if ((args.clone_flags & 0x00010000) == 0) {
    // Store the elapsed time:
    // - So we have a nonzero sentinel value
    // - So we can properly record the start of the fork,
    //   otherwise sometimes the exec shows up first.
    $ts = elapsed;
    @clones[tid] = $ts;
  }
}

tracepoint:syscalls:sys_exit_clone
{
  $task = (struct task_struct *)curtask;
  // Ensures that we don't record threads exiting
  if ($task->pid == $task->tgid) {
    // Don't process this clone unless we've recorded the `enter` side of it
    if (@clones[tid] != 0) {
      $ts = @clones[tid];
      @clones[tid] = 0;
      $child_pid = args.ret;
      printf("FORK: ts=%u,parent_pid=%d,child_pid=%d,parent_pgid=%d\n", $ts, $task->tgid, $child_pid, $task->real_parent->tgid);
    }
  }
}

tracepoint:syscalls:sys_enter_clone3
{
  $task = (struct task_struct *)curtask;
  // Ensures that we don't record a fork of a thread
  if ((args.uargs->flags & 0x00010000) == 0) {
    // Store the elapsed time:
    // - So we have a nonzero sentinel value
    // - So we can properly record the start of the fork,
    //   otherwise sometimes the exec shows up first.
    $ts = elapsed;
    @clones[tid] = $ts;
  }
}

tracepoint:syscalls:sys_exit_clone3
{
  $task = (struct task_struct *)curtask;
  // Ensures that we don't record a clone unless it's a process
  if ($task->pid == $task->tgid) {
    // Don't process this clone unless we've seen the `enter` side of it
    if (@clones[tid] != 0) {
      $ts = @clones[tid];
      @clones[tid] = 0;
      $child_pid = args.ret;
      printf("FORK: ts=%u,parent_pid=%d,child_pid=%d,parent_pgid=%d\n", $ts, $task->tgid, $child_pid, $task->real_parent->tgid);
    }
  }
}

///////////////////////////////////////////////////////////////////////////////
// Tracing execs
///////////////////////////////////////////////////////////////////////////////

tracepoint:syscalls:sys_enter_execve
{
  $task = (struct task_struct *)curtask;
  $ts = elapsed;
  printf("EXEC: ts=%u,pid=%d,ppid=%d,pgid=%d\n", $ts, $task->tgid, $task->real_parent->tgid, $task->group_leader->tgid);
  printf("EXEC_ARGS: ts=%u,pid=%d,", $ts, $task->tgid);
  join(args.argv);
}

///////////////////////////////////////////////////////////////////////////////
// Tracing exits
///////////////////////////////////////////////////////////////////////////////

tracepoint:sched:sched_process_exit
{
  $task = (struct task_struct *)curtask;
  // Ensures that we don't record threads exiting
  if ($task->pid == $task->tgid) {
    $ts = elapsed;
    printf("EXIT: ts=%u,pid=%d,ppid=%d,pgid=%d\n", $ts, $task->tgid, $task->real_parent->tgid, $task->group_leader->tgid);
  }
}

///////////////////////////////////////////////////////////////////////////////
// Tracing process groups and sessions
///////////////////////////////////////////////////////////////////////////////

tracepoint:syscalls:sys_exit_setsid
{
  $task = (struct task_struct *)curtask;
  $session = args.ret;
  $ts = elapsed;
  printf("SETSID: ts=%u,pid=%d,ppid=%d,pgid=%d,sid=%d\n", $ts, $task->tgid, $task->real_parent->tgid, $task->group_leader->tgid, $session);
}

tracepoint:syscalls:sys_exit_setpgid
{
  $task = (struct task_struct *)curtask;
  $ts = elapsed;
  printf("SETPGID: ts=%u,pid=%d,ppid=%d,pgid=%d\n", $ts, $task->tgid, $task->real_parent->tgid, args.ret);
}

END {
  clear(@clones);
}
